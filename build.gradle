plugins {
    id "groovy"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.3.1"
}

group 'com.athaydes.automaton'
version '1.0-SNAPSHOT'


sourceCompatibility = 1.7

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.3'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.3', {
        transitive = false
    }
    testCompile group: 'cglib', name: 'cglib-nodep', version: '2.2'
    testCompile group: 'com.athaydes', name: 'spock-reports', version: '1.2.7', {
        transitive = false
    }
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "Swing-Selectors",
                "Implementation-Version": version )
    }
}

task sourcesJar( type: Jar, dependsOn: classes ) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

publishing {
    publications {
        mavenJava( MavenPublication ) {
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = project.hasProperty( "bintrayUserName" ) ? bintrayUserName : null
    key = project.hasProperty( "bintrayApiKey" ) ? bintrayApiKey : null
    publications = [ "mavenJava" ]
    pkg {
        repo = 'maven'
        name = 'swing-selectors'
        licenses = [ 'Apache-2.0' ]
        desc = 'Swing-Selectors is a library for selecting Swing UI components'
        websiteUrl = 'https://github.com/renatoathaydes/Swing-Selectors'
        issueTrackerUrl = 'https://github.com/renatoathaydes/Swing-Selectors/issues'
        labels = [ 'groovy', 'java', 'swing', 'Automaton' ]
        publicDownloadNumbers = true
    }
}
bintrayUpload.dependsOn build, sourcesJar