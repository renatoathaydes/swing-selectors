plugins {
    id "groovy"
    id "maven"
    id "com.jfrog.bintray" version "1.4"
}

group 'com.athaydes.automaton'
version '1.0.1'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.3'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.3', {
        transitive = false
    }
    testCompile group: 'cglib', name: 'cglib-nodep', version: '2.2'
    testCompile group: 'com.athaydes', name: 'spock-reports', version: '1.2.7', {
        transitive = false
    }
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "Swing-Selectors",
                "Implementation-Version": version )
    }
}

/* Publishing config */

task javadocJar( type: Jar ) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar( type: Jar ) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

// add all the info required by Maven Central to the pom
configure( install.repositories.mavenInstaller ) {
    //repository(url: "file://$buildDir")
    pom.project {
        inceptionYear '2015'
        name project.name
        packaging 'jar'
        description project.description

        url 'https://github.com/renatoathaydes/Swing-Selectors'

        scm {
            connection 'git@github.com:renatoathaydes/Swing-Selectors.git'
            developerConnection 'git@github.com:renatoathaydes/Swing-Selectors.git'
            url 'https://github.com/renatoathaydes/Swing-Selectors'
        }

        licenses {
            license {
                name 'The Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'renatoathaydes'
                name 'Renato Athaydes'
                email 'renato@athaydes.com'
            }
        }
    }
}

bintray {
    user = project.hasProperty( "bintrayUserName" ) ? bintrayUserName : null
    key = project.hasProperty( "bintrayApiKey" ) ? bintrayApiKey : null
    configurations = [ 'archives' ]
    publish = true
    pkg {
        repo = 'maven'
        name = 'swing-selectors'
        licenses = [ 'Apache-2.0' ]
        desc = 'Swing-Selectors is a library for selecting Swing UI components'
        websiteUrl = 'https://github.com/renatoathaydes/Swing-Selectors'
        issueTrackerUrl = 'https://github.com/renatoathaydes/Swing-Selectors/issues'
        vcsUrl = 'https://github.com/renatoathaydes/swing-selectors.git'
        labels = [ 'groovy', 'java', 'swing', 'Automaton' ]
        publicDownloadNumbers = true

        //noinspection GroovyAssignabilityCheck
        version {
            name = project.version
            vcsTag = project.version
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = ossrhUsername
                password = ossrhPassword
                close = '1' // '0' to NOT close
            }
        }
    }
}
bintrayUpload.dependsOn build, sourcesJar